import fonts.fontAwesome;

import win.ui;
/*DSG{{*/
var winform = win.form(text="字幕总结";right=759;bottom=785)
winform.add(
button={cls="button";text="总结归纳去水";left=639;top=735;right=744;bottom=775;z=1};
edit={cls="edit";left=24;top=740;right=629;bottom=772;edge=1;multiline=1;z=2};
plus={cls="plus";left=7;top=10;right=39;bottom=40;bgcolor=-5197169;font=LOGFONT(h=-16);iconStyle={font=LOGFONT(h=-13;name='FontAwesome')};iconText='\uF0C5';notify=1;z=4};
plus2={cls="plus";left=47;top=11;right=79;bottom=41;bgcolor=-5197169;font=LOGFONT(h=-16);iconStyle={font=LOGFONT(h=-13;name='FontAwesome')};iconText='\uF014';notify=1;z=5};
richedit={cls="richedit";left=4;top=44;right=756;bottom=730;edge=1;multiline=1;vscroll=1;wrap=1;z=3}
)
/*}}*/

winform.plus.skin({
	background={
		default=0x668FB2B0;
		disabled=0xFFCCCCCC;
		hover=0xFF928BB3
	};
	color={
		default=0xFF000000;
		disabled=0xFF6D6D6D
	}
})

winform.plus2.skin({
	background={
		default=0x668FB2B0;
		disabled=0xFFCCCCCC;
		hover=0xFF928BB3
	};
	color={
		default=0xFF000000;
		disabled=0xFF6D6D6D
	}
})

import fsys.dlg
import thread.command

var listener = thread.command();
listener.$updateWb = function( str ){
	winform.richedit.appendText(str)
	winform.richedit.appendText('\n')
}

import inet.url
import bili
import fsys.config
var config = fsys.config()
winform.button.oncommand = function(id,event){
	var path = winform.edit.text
	var subText
	if(#path){
		var isUrl = inet.url.is(path,0x0/*_URLIS_URL*/)
		if(isUrl){
			var bvid = string.match(path,"bilibili\.com/video/(BV\w+)[\/]*")
			var bilibili = bili(config.winform.input_bili_token)
			var url = bilibili.getSubtitles(bvid)
			if(url){
				subText = bilibili.parse2vtt(url)
			}else {
				return ; 
			}
		}elseif(io.exist(path)){
			subText = string.load(path)
		}
	}
	else {
		path = fsys.dlg.open()
		if !path return ; 
		subText = string.load(path)	
	}
	winform.edit.text = path
	winform.button.disabled = true
	winform.button.text = "运行中"
	thread.invoke(
		function(subText,winform){
			import chatgpt.summary;
			import thread.command;
			import fsys.config
			var config = fsys.config()
			var gpt = chatgpt.summary(config.winform.input_apikey,subText,config.winform.input_proxy)
			for a,q in gpt.eachChunck(){
				if(!winform){
					break
				}
				thread.command.$updateWb(q)
			}
			winform.button.disabled = false
			winform.button.text = "总结归纳去水"
		},subText,winform
	)
}

winform.plus.oncommand = function(id,event){
	import win.clip
	win.clip.write(winform.richedit.text)
}

winform.plus2.oncommand = function(id,event){
	winform.richedit.text = ""
}

winform.show();
win.loopMessage();
return winform;